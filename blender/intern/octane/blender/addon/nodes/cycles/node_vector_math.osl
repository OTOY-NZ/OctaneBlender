// #include "node_math.h"

float safe_divide(float a, float b)
{
  return (b != 0.0) ? a / b : 0.0;
}

vector safe_divide(vector a, vector b)
{
  return vector((b[0] != 0.0) ? a[0] / b[0] : 0.0,
                (b[1] != 0.0) ? a[1] / b[1] : 0.0,
                (b[2] != 0.0) ? a[2] / b[2] : 0.0);
}

float safe_modulo(float a, float b)
{
  return (b != 0.0) ? fmod(a, b) : 0.0;
}

float safe_floored_modulo(float a, float b)
{
  return (b != 0.0) ? a - floor(a / b) * b : 0.0;
}

float fract(float a)
{
  return a - floor(a);
}

/* See: https://www.iquilezles.org/www/articles/smin/smin.htm. */
float smoothmin(float a, float b, float c)
{
  if (c != 0.0) {
    float h = max(c - abs(a - b), 0.0) / c;
    return min(a, b) - h * h * h * c * (1.0 / 6.0);
  }
  else {
    return min(a, b);
  }
}

float pingpong(float a, float b)
{
  return (b != 0.0) ? abs(fract((a - b) / (b * 2.0)) * b * 2.0 - b) : 0.0;
}

float safe_sqrt(float a)
{
  return (a > 0.0) ? sqrt(a) : 0.0;
}

float safe_log(float a, float b)
{
  return (a > 0.0 && b > 0.0) ? log(a) / log(b) : 0.0;
}

vector project(vector v, vector v_proj)
{
  float lenSquared = dot(v_proj, v_proj);
  return (lenSquared != 0.0) ? (dot(v, v_proj) / lenSquared) * v_proj : vector(0.0);
}

vector snap(vector a, vector b)
{
  return floor(safe_divide(a, b)) * b;
}

/* Adapted from GODOT-engine math_funcs.h. */
float wrap(float value, float max, float min)
{
  float range = max - min;
  return (range != 0.0) ? value - (range * floor((value - min) / range)) : min;
}

point wrap(point value, point max, point min)
{
  return point(wrap(value[0], max[0], min[0]),
               wrap(value[1], max[1], min[1]),
               wrap(value[2], max[2], min[2]));
}

/* Built in OSL faceforward is `(dot(I, Nref) > 0) ? -N : N;` which is different to
 * GLSL `dot(Nref, I) < 0 ? N : -N` for zero values. */
point compatible_faceforward(point vec, point incident, point reference)
{
  return dot(reference, incident) < 0.0 ? vec : -vec;
}

matrix euler_to_mat(point euler)
{
  float cx = cos(euler[0]);
  float cy = cos(euler[1]);
  float cz = cos(euler[2]);
  float sx = sin(euler[0]);
  float sy = sin(euler[1]);
  float sz = sin(euler[2]);
  matrix mat = matrix(1.0);
  mat[0][0] = cy * cz;
  mat[0][1] = cy * sz;
  mat[0][2] = -sy;
  mat[1][0] = sy * sx * cz - cx * sz;
  mat[1][1] = sy * sx * sz + cx * cz;
  mat[1][2] = cy * sx;
  +mat[2][0] = sy * cx * cz + sx * sz;
  mat[2][1] = sy * cx * sz - sx * cz;
  mat[2][2] = cy * cx;
  return mat;
}


shader node_vector_math(int math_type = 0,
                        vector Vector1 = vector(0.0, 0.0, 0.0),
                        vector Vector2 = vector(0.0, 0.0, 0.0),
                        vector Vector3 = vector(0.0, 0.0, 0.0),
                        float Scale = 1.0,
                        output vector Vector = vector(0.0, 0.0, 0.0))
{
    if (math_type == 0) {  // Add
        Vector = Vector1 + Vector2;
    }
    else if (math_type == 1) {  // Subtract
        Vector = Vector1 - Vector2;
    }
    else if (math_type == 2) {  // Multiply
        Vector = Vector1 * Vector2;
    }
    else if (math_type == 3) {  // Divide
        Vector = safe_divide(Vector1, Vector2);
    }
    else if (math_type == 4) {  // Cross Product
        Vector = cross(Vector1, Vector2);
    }
    else if (math_type == 5) {  // Project
        Vector = project(Vector1, Vector2);
    }
    else if (math_type == 6) {  // Reflect
        Vector = reflect(Vector1, normalize(Vector2));
    }
    else if (math_type == 7) {  // Refract
        Vector = refract(Vector1, normalize(Vector2), Scale);
    }
    else if (math_type == 8) {  // Faceforward
        Vector = compatible_faceforward(Vector1, Vector2, Vector3);
    }
    else if (math_type == 9) {  // Multiply Add
        Vector = Vector1 * Vector2 + Vector3;
    }
    else if (math_type == 10) {  // Dot Product
        Vector = dot(Vector1, Vector2);
    }
    else if (math_type == 11) {  // Distance
        Vector = distance(Vector1, Vector2);
    }
    else if (math_type == 12) {  // Length
        Vector = length(Vector1);
    }
    else if (math_type == 13) {  // Scale
        Vector = Vector1 * Scale;
    }
    else if (math_type == 14) {  // Normalize
        Vector = normalize(Vector1);
    }
    else if (math_type == 15) {  // Snap
        Vector = snap(Vector1, Vector2);
    }
    else if (math_type == 16) {  // Floor
        Vector = floor(Vector1);
    }
    else if (math_type == 17) {  // Ceil
        Vector = ceil(Vector1);
    }
    else if (math_type == 18) {  // Modulo
        Vector = fmod(Vector1, Vector2);
    }
    else if (math_type == 19) {  // Wrap
        Vector = wrap(Vector1, Vector2, Vector3);
    }
    else if (math_type == 20) {  // Fraction
        Vector = Vector1 - floor(Vector1);
    }
    else if (math_type == 21) {  // Absolute
        Vector = abs(Vector1);
    }
    else if (math_type == 22) {  // Minimum
        Vector = min(Vector1, Vector2);
    }
    else if (math_type == 23) {  // Maximum
        Vector = max(Vector1, Vector2);
    }
    else if (math_type == 24) {  // Sine
        Vector = sin(Vector1);
    }
    else if (math_type == 25) {  // Cosine
        Vector = cos(Vector1);
    }
    else if (math_type == 26) {  // Tangent
        Vector = tan(Vector1);
    }
    else {
        // warning("%s", "Unknown vector math operator!");
    }
}